start: (directive | instruction | label)*

// ------------------------------------------------------------------------- //
//
//
//
//
directive: ".org" INTEGER -> org

// ------------------------------------------------------------------------- //
//
//
//
//

?link: /[_a-z]+/

label: link ":" -> label

instruction: /j(eq|gt|le)?/ link -> jump
           | "push" reg -> push
           | "pop" reg -> pop
           | "ld" reg "," "[" reg "]" -> ld
           | "st" "[" reg "]" "," reg -> st
           | "mov" reg "," (reg | INTEGER) -> mov
           | "movs" (reg | sreg) "," (reg | sreg) -> movs
           | "sub" reg "," reg "," (reg | INTEGER) -> sub
           | "add" reg "," reg "," (reg | INTEGER) -> add
           | "emit" -> emit
           | "wait" -> wait
           | "ret" -> ret
           | "call" link -> call
           | "cmp" reg "," reg -> cmp

?reg: /r[0-9]+/
    | /x[0-9]+/
    | BLINK

?sreg: NREG

BLINK: "blink"
NREG: "N" 

%import common.WS
%import common.INT -> INTEGER
%ignore WS
%ignore ";" /.*\n?/
